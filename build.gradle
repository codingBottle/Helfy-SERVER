plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.codingbottle'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'application'
    apply plugin: 'distribution'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'jacoco'

    group = 'com.codingbottle'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }


    jacoco {
        toolVersion = '0.8.10'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        implementation  'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation group: 'com.google.firebase', name: 'firebase-admin', version: '8.0.1'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'com.github.napstr:logback-discord-appender:1.0.0'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'com.mysql:mysql-connector-j'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    plugins.withType(JavaPlugin).tap {
        configureEach {
            java {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }

            compileJava {
                options.encoding = 'UTF-8'
            }

            idea {
                module {
                    outputDir = file("$buildDir/classes/main/")
                    testOutputDir = file("$buildDir/classes/test/")
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
    def QueryDslDomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        QueryDslDomains.add(qPattern + '*')
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application*",
                            "**/*Config*",
                            "**/*Dto*",
                            "**/*Request*",
                            "**/*Response*",
                            "**/*Interceptor*",
                            "**/*Exception*"
                    ] + QueryDslDomains)
                })
        )
    }
    finalizedBy jacocoTestCoverageVerification
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

